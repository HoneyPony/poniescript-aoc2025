var ZERO    = "0"[0];
var NINE    = "9"[0];

fun is_digit(c: int) -> bool {
    c >= ZERO and c <= NINE
}

fun is_not_digit(c: int) -> bool {
    c < ZERO or c > NINE
}

class Text {
    // Note: Putting the read_text() directly here lead to an internal C error.
    var text: StrBuf = "";
    var idx = 0;

    fun number() -> (int, bool) {
        if idx >= text.length { return (0, false); }

        var number = 0;
        while is_digit(text[idx]) {
            number = (number * 10 + text[idx] - ZERO);
            idx = idx + 1;
        }

        while idx < text.length and is_not_digit(text[idx]) { idx = idx + 1; };

        (number, true)
    }

    fun pair() -> (vec2i, bool) {
        var a = number();
        var b = number();
        if a.1 and b.1 {
            return ((a.0, b.0), false);
        }
        return ((0, 0), true);
    }
}

fun get_text() -> Text { new Text { text: read_file("input.txt") } }

fun init() {
    var points: DynArray[vec2i] = [];
    //var pairs: DynArray[(vec2i, vec2i)] = [];
    var text = get_text();

    loop {
        var next = text.pair();
        if next.1 { break; };

        points.push(next.0);
    }

    fun abs(x: int) -> int {
        if x < 0 { -x } else { x } 
    }

    var best = 0;

    for i in 0..points.length {
        for j in i + 1..points.length {
            var a = points[i];
            var b = points[j];
            
            var area = (abs(a.0 - b.0) + 1) * (abs(a.1 - b.1) + 1);
            //print(a, " ", b, " => ", area);
            if area > best {
                best = area;
            }
        }
    }

    print(best);
}
