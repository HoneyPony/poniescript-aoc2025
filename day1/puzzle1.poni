var ZERO = "0"[0];
var NINE = "9"[0];

fun is_digit(char: int) -> bool {
    char >= ZERO and char <= NINE
}

fun circular(dial: int) -> int {
    while dial < 0 {
        dial = dial + 100;
    }
    while dial >= 100 {
        dial = dial - 100;
    }
    dial
}

fun ne(x: int, y: int) -> bool {
    x < y or x > y
}

fun init() {
    var contents = read_file("input.txt");

    var L = "L"[0];
    var R = "R"[0];
    var nl = "\n"[0];

    var i = 0;

    // The dial. We add the numbers to it, then count the number of 0 hits.
    var dial = 50;
    // The number of zero hits. Possibly incremented after each turn.
    var zero_hits = 0;

    while i < contents.length {
        // First, read until we hit the next L or R.
        while i < contents.length and ne(contents[i], L) and ne(contents[i], R) {
            i = i + 1;
        }
        if i >= contents.length { break; }

        var direction = lerp(-1, 1, contents[i] == R);

        i = i + 1;

        var number = 0;
        while i < contents.length and is_digit(contents[i]) {
            number = number * 10;
            number = number + (contents[i] - ZERO);

            i = i + 1;
        }

        var prev = dial;
        dial = circular(dial + direction * number);
        print("dial: ", prev, " -> ", dial, " after turning ", lerp("left", "right", direction > 0), " by ", number);
        if dial == 0 {
            zero_hits = zero_hits + 1;
        }
    }

    print("zero hits: ", zero_hits);
}