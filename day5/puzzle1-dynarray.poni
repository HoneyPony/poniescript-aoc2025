var ZERO = "0"[0];
var NINE = "9"[0];

fun is_digit(c: int) -> bool {
    c >= ZERO and c <= NINE
}

fun includes(range: vec2i, value: int) -> bool {
    value >= range.0 and value <= range.1
}

fun is_in_any_range(vals: DynArray[vec2i], value: int) -> bool {
    for i in 0..vals.length {
        if includes(vals[i], value) { return true; }
    }

    false
}

fun init() {
    var text = read_file("input.txt");
    var i = 0;

    var ranges: DynArray[vec2i] = [];

    loop {
        // This is the empty line if there's no digit.
        if is_digit(text[i]) {} else { break; }

        var item = (0, 0);

        while is_digit(text[i]) {
            item.0 = (item.0 * 10 + text[i] - ZERO);
            i = i + 1;
        }
        // Consume the '-'
        i = i + 1;
        while is_digit(text[i]) {
            item.1 = (item.1 * 10 + text[i] - ZERO);
            i = i + 1;
        }
        // Consume the newline
        i = i + 1;

        // Push the range
        ranges.push(item);
    }

    // Skip the blank line
    i = i + 1;

    var sum = 0;

    loop {
        if i >= text.length { break; }
        if is_digit(text[i]) {} else { break; }
        
        var next = 0;
        while is_digit(text[i]) {
            next = (next * 10 + text[i] - ZERO);
            i = i + 1;
        }
        // Skip the newline
        i = i + 1;

        if is_in_any_range(ranges, next) {
            sum = sum + 1;
        }
    }

    print("sum = ", sum);
}
