var ZERO = "0"[0];
var NINE = "9"[0];

fun is_digit(c: int) -> bool {
    c >= ZERO and c <= NINE
}

fun merge(a: vec2i, b: vec2i) -> (vec2i, bool) {
    fun _merge(a: vec2i, b: vec2i) -> (vec2i, bool) {
        if a.0 <= b.0 and a.1 >= b.1 {
            return (a, true);
        }
        if a.0 <= b.1 + 1 and a.1 > b.1 {
            return ((b.0, a.1), true);
        }
        return ((0, 0), false);
    }

    var x = _merge(a, b);
    var y = _merge(b, a);
    if x.1 { return x; }
    if y.1 { return y; }
    return ((0, 0), false);
}

class Link {
    // Note to self: 'range' is currently reserved, that seems bad..?
    var item: vec2i = (0, 0);
    var next: Link? = nil;

    fun push(new_item: vec2i) {
        next = new Link {
            item: item,
            next: next,
        };

        // We take over the newest item.
        item = new_item;        
    }

    fun includes(value: int) -> bool {
        value >= item.0 and value <= item.1
    }
}

class LinkedList {
    var head: Link? = nil;

    fun push(item: vec2i) {
        var old_head = head else {
            head = new Link { item: item, next: nil };
            return;
        };
        old_head.push(item);
    }

    fun is_in_any_range(value: int) -> bool {
        var iter = head;

        loop {
            var item = iter else { break; };
            if item.includes(value) { return true; }

            iter = item.next;
        }

        false
    }

    fun try_merge(new_val: vec2i) -> bool {
        var iter = head;

        loop {
            var item = iter else { break; };
            var m = merge(item.item, new_val);
            if m.1 {
                item.item = m.0;
                return true;
            }

            iter = item.next;
        }

        false
    }

    fun length() -> int {
        var result = 0;
        var iter = head;
        loop {
            var item = iter else { break; };
            result = result + 1;

            iter = item.next;
        }
        result
    }
}

fun init() {
    var text = read_file("input.txt");
    var i = 0;

    var ranges = new LinkedList{};

    loop {
        //print(text[i], is_digit(text[i]));
        //print("hello");
        // This is the empty line if there's no digit.
        if is_digit(text[i]) {} else { break; }

        var item = (0, 0);

        while is_digit(text[i]) {
            item.0 = (item.0 * 10 + text[i] - ZERO);
            i = i + 1;
        }
        // Consume the '-'
        i = i + 1;
        while is_digit(text[i]) {
            item.1 = (item.1 * 10 + text[i] - ZERO);
            i = i + 1;
        }
        // Consume the newline
        i = i + 1;

        // Push the range
        if ranges.try_merge(item) { print("merged!"); }
        else {
            ranges.push(item);
            print("new range!");
        }
        //print("pushing item ", item)
    }

    loop {
        print("--- new iter ---");
        var new_list = new LinkedList{};

        var iter = ranges.head;

        loop {
            var item = iter else { break; };
            
            if new_list.try_merge(item.item) { print("merged!"); }
            else {
                new_list.push(item.item);
                print("new range!");
            }

            iter = item.next;
        }

        if new_list.length() == ranges.length() {
            ranges = new_list;
            break;
        }

        ranges = new_list;
    }

    var iter = ranges.head;
    var sum = 0;

    loop {
        var item = iter else { break; };
        
        sum = sum + item.item.1 - item.item.0 + 1;

        iter = item.next;
    }

    print(sum);
}
